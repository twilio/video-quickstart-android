buildscript {
    ext.kotlin_version = '1.5.0'
    ext.versions = [
            'java'               : JavaVersion.VERSION_1_8,
            'androidGradlePlugin': '4.0.1',
            'googleServices'     : '3.2.1',
            'compileSdk'         : 31,
            'buildTools'         : '30.0.2',
            'minSdk'             : 21,
            'targetSdk'          : 31,
            'supportLibrary'     : '26.1.0',
            'constraintLayout'   : '1.1.3',
            'firebase'           : '10.0.1',
            'retrofit'           : '2.0.0-beta4',
            'okhttp'             : '3.6.0',
            'ion'                : '2.1.8',
            'videoAndroid'       : '7.0.6',
            'audioSwitch'        : '1.1.4'
    ]

    ext.getSecretProperty = { key, defaultValue ->
        def value = System.getenv(key)

        if (value == null) {
            Properties properties = new Properties()
            if (project.rootProject.file('local.properties').exists()) {
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                value = properties.getProperty(key)
            }
        }

        if (value == null) {
            value = defaultValue
        }

        return value
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.1.0"
}
apply plugin: "com.diffplug.gradle.spotless"
spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        targetExclude '**/build/**/settings.gradle'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        target '**/*.java'
        googleJavaFormat().aosp()
    }
    kotlin {
        target '**/*.kt'
        ktlint()
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}
