buildscript {
    ext.kotlin_version = '1.6.21'
    ext.versions = [
            'java'               : JavaVersion.VERSION_11,
            'androidGradlePlugin': '8.3.1',
            'googleServices'     : '4.3.14',
            'compileSdk'         : 34,
            'buildTools'         : '30.0.2',
            'minSdk'             : 21,
            'targetSdk'          : 34,
            'supportLibrary'     : '26.1.0',
            'constraintLayout'   : '1.1.3',
            'firebase'           : '10.0.1',
            'retrofit'           : '2.0.0-beta4',
            'okhttp'             : '3.6.0',
            'ion'                : '2.1.8',
            'videoAndroid'       : '7.7.0',
            'audioSwitch'        : '1.1.8'
    ]

    ext.getSecretProperty = { key, defaultValue ->
        def value = System.getenv(key)

        if (value == null) {
            Properties properties = new Properties()
            if (project.rootProject.file('local.properties').exists()) {
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                value = properties.getProperty(key)
            }
        }

        if (value == null) {
            value = defaultValue
        }

        return value
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.diffplug.spotless" version "6.19.0"
}
apply plugin: "com.diffplug.spotless"
spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        targetExclude '**/build/**/settings.gradle'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        target '**/*.java'
        googleJavaFormat().aosp()
    }
    kotlin {
        target '**/*.kt'
        ktlint()
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
